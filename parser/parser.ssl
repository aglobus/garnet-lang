%  PT Parser.ssl	v1.01
%
% Date:  30 Jun 1980
%	 (revised 7 December 2009)
%
% Author:  Alan Rosselet
%
% Abstract
%
%     This module parses the syntax token stream emitted by the scanner
%   and emits a postfix stream of tokens to the semantic analyzer
%   module.  The parser detects and repairs syntax errors so that the
%   token stream emitted to the semantic analyzer is syntactically
%   legal (except for the special case of declaration phase errors - see
%   comment in the Block routine below).

Input :
	% Keywords must be first both here and in stdIdentifiers,
	% and they must appear in the same order

	pDiv			'div'
	firstKeywordToken = pDiv
	pMod			'mod'
	pOr			'or'
	pAnd			'and'
	pThen			'then'
	pElse			'else'
	pOf			'of'
	pEnd			'end'
	pDo			'do'
	pArray			'array'
	pFile			'file'
	pPacked			'packed'
	pVar			'var'
	pType			'type'
	pIf			'if'
	pCase			'case'
	pWhile			'while'
  pUsing      'using'
  pVal        'val'
  pDef        'def'
  pUnless     'unless'
  pElsif      'elsif'
  pBreak      'break'
  pWhen       'when'
  pModule     'module'
	lastKeywordToken = pModule 

	pIdentifier 
	firstCompoundInputToken = pIdentifier
	pInteger
	pLiteral
	lastCompoundInputToken = pLiteral

  pDoubleEquals '=='
  pNot          '!'
  pQuestion     '?'
  pDollar       '$'
  pHash         '#'
	pNewLine
	pEndFile
	pPlus			'+'
	pMinus			'-'
	pStar			'*'
	pColonEquals		':='
	pDot			'.'
	pComma			','
	pSemicolon		';'
	pColon			':'
	pEquals			'='
	pNotEqual		'<>'
	pLess			'<'
	pLessEqual		'<='
	pGreaterEqual		'>='
	pGreater		'>'
	pLeftParen		'('
	pRightParen		')'
	pLeftBracket		'['
	pRightBracket		']'
	pDotDot			'..'
	lastSyntaxToken = pDotDot;

Output :
	sIdentifier 
	firstSemanticToken = sIdentifier
	firstCompoundSemanticToken = sIdentifier
	sInteger
	sLiteral
	lastCompoundSemanticToken = sLiteral

	sProgram
	sParmBegin
	sParmEnd
	sConst
	sType
	sVar
	sProcedure
	sBegin
	sEnd
	sNegate
	sArray
	sPacked
	sFile
	sRange
	sCaseStmt
	sCaseEnd
	sLabelEnd
	sExpnEnd
	sNullStmt
	sAssignmentStmt
	sSubscript
	sCallStmt
	sFieldWidth
	sIfStmt
	sThen
	sElse
	sWhileStmt
  sPublic
  sModule
  sDoStmt
  sBreakIf
  sSubstring
  sLength
  sIndex
	sEq
	sNE
	sLT
	sLE
	sGT
	sGE
	sAdd
	sSubtract
	sMultiply
	sDivide
	sModulus
	sInfixOr
	sOr
	sInfixAnd
	sAnd
	sNot
	sNewLine
	lastSemanticToken = sNewLine;


rules

Program :
	'using'  .sProgram
	% program name
	pIdentifier
  .sIdentifier
  % Read in multiple identifiers that can follow 'using'
  {[
    | ',':
      pIdentifier .sIdentifier
    | *:
      >
  ]}
	.sParmEnd
  % Call our new rule to read declarations and statements
  @DecsAndStmts;

% New rule that reads in a series of declarations followed by a series of statements
DecsAndStmts :
  @Declarations
  @Statements;

% Looping match rule that looks for one of the following keywords
% Depending on what we read, follow through and call the appropriate rule
% If we see none then exit
Declarations : 
	{[
	    | 'val':
		.sConst
		@ConstantDefinitions
	    | 'type':
		.sType
		@TypeDefinitions
	    | 'var':
		.sVar
		@VariableDeclarations
      | 'module': % Module's structure is simple enough to not warrant a new rule
    .sModule
    pIdentifier .sIdentifier
    @DecsAndStmts 'end'
	    | 'def':
      @HandleProcedure
      | ';':
      .sNullStmt
	    | *:
      >
	]};

HandleProcedure :
		.sProcedure % procedure name

    % If we read in the public asterisk
    % emit the identifier then public semantic token before
    % consuming the identifier
    % For any other case (i.e not public)
    % consume and then emit
    [
    | '*':
      .sIdentifier
      .sPublic
      pIdentifier
    | *:
      pIdentifier  .sIdentifier
    ]
    % Handle the arguments to the procedure
		@ProcedureHeading

    % A procedure body is a series of delcarations followed by a series of statements
    % Call the rule we have to deal with that
		@DecsAndStmts
    'end';


Statements :
  .sBegin
	{[
	    | pIdentifier:
		@AssignmentOrCallStmt
    % Begin isn't a part of our Garnet, so we can remove it
    % Commented out for now
	  %   | 'begin':
		% @BeginStmt
	    | 'if':
		@IfStmt
	    | 'while':
		@WhileStmt
	    | 'case':
		@CaseStmt
	    | 'do':
		@DoStmt
      | 'unless':
    @UnlessStmt
      | ';':
	    | *:
		% .sNullStmt
      .sEnd
      >
	]};

ConstantDefinitions :
	pIdentifier  .sIdentifier
	@ConstantValue;

  % In Garnet, a constant can only have one definition
  % No need to read in more

	% {[
	%     | pIdentifier:
	% 	.sIdentifier
	% 	@ConstantValue
	%     | *:
  %   >
	% ]};

ConstantValue :
	'='
	[
	    | pInteger:
		.sInteger
	    | pIdentifier:
		.sIdentifier
	    | '-':
		@UnsignedIntegerConstant
		.sNegate
	    | '+':
		@UnsignedIntegerConstant
	    | pLiteral:
		.sLiteral
	];

UnsignedIntegerConstant :
	[
	    | pIdentifier:
		.sIdentifier
	    | pInteger:
		.sInteger
	];

TypeDefinitions :
	pIdentifier  .sIdentifier
	':'  @TypeBody;

  % Again, like constants, only one definition
  % No need to loop and read more

	% {[
	%     | pIdentifier:
	% 	.sIdentifier
	% 	':'  @TypeBody
	%     | *:
	% 	>
	% ]};

TypeBody :
	[
	    | 'packed':
		'array'  .sArray  .sPacked
		'['  @SimpleType  ']'
		'of'  @SimpleType
	    | 'array':
		.sArray
		'['  @SimpleType  ']'
		'of'  @SimpleType
	    | 'file':
		.sFile
		'of'  @SimpleType
	    | *:
		@SimpleType
	];

SimpleType :
	% A simple type is a named type (possibly predefined)
	% or a subrange type specification
	[
	    | pIdentifier:
		.sIdentifier
		[
		    | '..':
			.sRange		%% this may be unnecessary
			@OptionallySignedIntegerConstant
		    | *:
		]
	    | *:
		@OptionallySignedIntegerConstant
		'..'  .sRange
		@OptionallySignedIntegerConstant
	];

OptionallySignedIntegerConstant :
	[
	    | '-':
		@UnsignedIntegerConstant
		.sNegate
	    | '+':
		@UnsignedIntegerConstant
	    | *:
		@UnsignedIntegerConstant
	];

VariableDeclarations :
	% Accept one or more variable declarations.
	pIdentifier  .sIdentifier

  @OptVarDecl

  % We want to read in multiple variables
  % But only they should have only one type
  % e.g. something like var a,b : string : integer is invalid
  % So read the typebody and exit

	':'  @TypeBody;
	% {[
	%     | pIdentifier:
	% 	.sIdentifier
	% 	':'  @TypeBody
	%     | *:
	% 	>
	% ]};

  % New rule to read in multiple comma separated declarations
  % e.g. a,b,c... etc
OptVarDecl :
  {[
    | ',':
  pIdentifier
  .sVar
  .sIdentifier
    | *:
  >
  ]};

ProcedureHeading :
	% Accept zero or more procedure formal parameter declarations.
  % In Garnet, an empty procedure heading must have a set of
  % empty parenthesis. Unlike PT where they're optional
  % So the bulk of logic goes under the '(' case
  % if we see anything other than the left parenthesis, don't do anything


  '('
  [
    | ')':
    | *:
                {
                    % formal parameter identifier
                    [
                        | 'var':
                            pIdentifier  .sIdentifier
                            .sVar
                        | *:
                            pIdentifier  .sIdentifier
                    ]
                    ':'
                    % type identifier
                    pIdentifier  .sIdentifier
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
    ]
    .sParmEnd;

Statement :
	[
	    | pIdentifier:
		@AssignmentOrCallStmt
	    | 'begin':
		@BeginStmt
	    | 'if':
		@IfStmt
	    | 'while':
		@WhileStmt
	    | 'case':
		@CaseStmt
	    | 'do':
		@DoStmt
      | 'unless':
    @UnlessStmt
	    | *:
		.sNullStmt
      >
	];

AssignmentOrCallStmt :
	[
	    | '=':
		.sAssignmentStmt
		.sIdentifier	% identifier accepted in the Statement rule
		@Expression
		.sExpnEnd
	    | '[':
		.sAssignmentStmt
		.sIdentifier
		.sSubscript
		@Expression
		.sExpnEnd
		']'  '='
		@Expression
		.sExpnEnd
	    | *:
		@CallStmt
	];

CallStmt :
	.sCallStmt
	.sIdentifier	% procedure identifier accepted in the Statement rule
	% accept zero or more actual parameters
	[
	    | '(':
		{
		    @Expression
		    .sExpnEnd
		    % allow a field width specification to occur in calls
		    % to the standard procedures write and writeln
		    [
			| ':':
			    .sFieldWidth
			    @Expression
			    .sExpnEnd
			| *:
		    ]
		    [
			| ',':
			| *:
			    >
		    ]
		}
		')'
	    | *:	% no actual parameters
	]
	.sParmEnd;

% BeginStmt :
% 	.sBegin
% 	{
% 	    @Statement
% 	    [
% 		| ';':
% 		| 'end':
% 		    .sEnd
% 		    >
% 	    ]
% 	};

IfStmt :
  % The heading is pretty straightforward
  % Read in the expression, emit the tokens
  % and do the same for 'then'
	.sIfStmt
	@Expression
	.sExpnEnd
	'then'  .sThen


  % The body of our if statement is a sequence of statements
  % Call the rule we have to take care of it
  @Statements

  % After the sequence of statements, what we do next is dependent
  % on the next keyword. If we see an elsif, emit the sElse and
  % recursively call IfStmt while nesting it in sBegin and sEnd
  % this way, we translate an elseif to its equivalent if + else form

  % If we see an 'else' emit the sElse token and read in its body,
  % which is a series of statements. There is only one else clause
  % per if statement, so after the statements are done we can read
  % the ending 'end' keyword and exit

  % If our if statement has no elsifs or elses, then we expect an
  % 'end' keyword and exit
  [
    | 'elsif':
      .sElse
      % .sBegin
      @IfStmt
      % .sEnd
    | 'else':
      .sElse
      @Statements
      'end'
      >
    | 'end':
      >
  ];

  % Original body
	% @Statement
	% [
	%     | 'else':
	% 	.sElse
	% 	@Statement
  %     | 'end':
	%     | *:
	% ];

% Same as IfStmt except .sNot emitted after @Expression
UnlessStmt :
   % An unless statement is equivalent to if not
   % So the first few statements are the same as @IfStmt
   % Except we output sNot after @Expression
	.sIfStmt
	@Expression
  .sNot
	.sExpnEnd
	'then'  .sThen

  % Same as if, our unless body is 0 or more statements
  @Statements

  % Unlike if statements, an unless can only contain an
  % optional else block, which we handle in the following
  % if we see an else, emit sElse and read in the statements
  % otherwise on 'end', pass through and terminate
  [
    | 'else':
    .sElse
    @Statements
    'end'
    | 'end':
  ];


CaseStmt :
        .sCaseStmt
        @Expression
        .sExpnEnd
        {[
            | 'when':
                @CaseAlternative
            | *:
                >
        ]}
        [
            | 'else' :
                .sElse
                @Statements
            | *:
        ]
        'end'
        .sCaseEnd;

CaseAlternative :
        {
            @OptionallySignedIntegerConstant
            [
                | ',':
                | *:
                    >
            ]
        }
        .sLabelEnd
        'then'
        @Statements;

WhileStmt :
        .sWhileStmt
        @Expression
        .sExpnEnd
        'do'
        @Statements
        'end';

WhileStmt :
	.sWhileStmt
	@Expression
	.sExpnEnd
	'do'
	@Statements;

DoStmt :
  .sDoStmt
  @Statements
  'break'
  'if'
  .sBreakIf
  @Expression
	.sExpnEnd
  @Statements
  'end';

% RepeatStmt :
% 	.sRepeatStmt
% 	{
% 	    @Statement
% 	    [
% 		| ';':
% 		| 'until':
% 		    .sRepeatEnd
% 		    >
% 	    ]
% 	}
% 	@Expression
% 	.sExpnEnd;

Expression :
	@SimpleExpression
	[
	    | '==':
		@SimpleExpression  .sEq
	    | '!=':
		@SimpleExpression  .sNE
	    | '<':
		@SimpleExpression  .sLT
	    | '<=':
		@SimpleExpression  .sLE
	    | '>':
		@SimpleExpression  .sGT
	    | '>=':
		@SimpleExpression  .sGE
	    | *:
	];

SimpleExpression :
	[
	    | '+':
		@Term
	    | '-':
		@Term  .sNegate
	    | *:
		@Term
	]
	{[
	    | '+':
		@Term  .sAdd
	    | '-':
		@Term  .sSubtract
	    | 'or':
		.sInfixOr  @Term  .sOr
	    | *:
		>
	]};

Term :
  @SubTerm
	{[
	    | '*':
		@SubTerm  .sMultiply
	    | 'div':
		@SubTerm  .sDivide
	    | 'mod':
		@SubTerm  .sModulus
      | '?':
    @Subterm .sIndex
	    | 'and':
		.sInfixAnd  @SubTerm  .sAnd
	    | *:
		>
	]};

SubTerm :
        @Factor
        {[
          | '$':
            @Range .sSubstring
          | *:
            >
        ]};

Range :
      @Factor
      '..'
      @Factor;

Factor :
	[
	    | pIdentifier:
		.sIdentifier
		@IdentifierExtension
	    | pInteger:
		.sInteger
	    | '(':
		@Expression  ')'
	    | '!':
		@Factor
		.sNot
      | '#':
    @Factor
    .sLength
	    | pLiteral:
		.sLiteral
	    | 'file':
		.sFile '(' @Expression ')'
		.sExpnEnd
	];

IdentifierExtension :
	% Handle subscripts and function actual parameter lists
	[
	    | '[':
		.sSubscript
		@Expression  ']'
		.sExpnEnd
	    | '(':
		.sParmBegin
		@Expression  ')'
		.sExpnEnd
		.sParmEnd
	    | *:
	];

end
